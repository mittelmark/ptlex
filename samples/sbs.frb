%{
    $query_id = ""
    $query_length = 0
    $hit_id = ""
    $score = -1
    $evalue = -1
    $hit = 0
    $position = -1 ;
%}

%option buffersize=256
%option nodefault=true

%s QUERY
%s HITS
%%
<*>BLAST[XNP] {
    $hit = 0 
    $position = $yychar  
    BEGIN "QUERY"
}
<QUERY>Query=[ ][^ \t\n]+ {
      $query_id = $yytext[7,$yytext.length-7]
}
<QUERY>([0-9]+[ ]letters) {
    $query_length = $yytext[1,$yytext.length-7]
    dumpPosition
    
}
<QUERY>Sequences[ ]producing[ ]significant[ ]alignments {
    BEGIN "HITS"
}
<HITS>>[^ \t\n]+ {
   $hit_id = $yytext[1,$yytext.length-1]

}
<HITS>[ ]Score[ ]=[ ]+[0-9]+ {
    $score = $yytext[$yytext.rindex(" "),$yytext.length]
}  
<HITS>[ ]Expect[^ ]*[ ]=[ ]+[-\.e0-9]+ {
   $evalue = $yytext[$yytext.rindex(" "),$yytext.length] 
   if /\Ae/.match($evalue)
        $evalue = "1"+$evalue
   end
   dumpHit
}
<*>(.|\n) { # do nothing }

%%
def dumpPosition 
    print "insert into positions (query_id,query_length,position) values ('#{$query_id}',#{$query_length},#{$position});\n"
end   
def dumpHit
    $hit += 1
    print "insert into hits (query_id,hit_id,hit,score,evalue) values ('#{$query_id}','#{$hit_id}',#{$hit},#{$score},#{$evalue});\n"    
end

def usage
    puts "Usage: #{$0} <filename>"
    exit 0
end
if __FILE__ == $0
    puts __FILE__
    if ARGV.length != 1
        usage
    end
    yylex(ARGV[0])
end

